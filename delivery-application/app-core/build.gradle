plugins {
    id 'common.java-conventions'
    id 'module.java-conventions'
}

dependencies {
    // ***** Spring *****
    developmentOnly libs.spring.boot.devtools
    implementation libs.spring.boot.starter.freemarker
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.actuator
    implementation libs.hibernate.validator

    // ***** Database *****
    implementation libs.spring.boot.starter.data.jdbc
    implementation libs.spring.boot.starter.data.jpa
    runtimeOnly libs.postgresql

    // ***** Utility *****
    runtimeOnly libs.micrometer.prometheus

    // ***** Tests *****
    testImplementation libs.junit.jupiter

    // ***** Project Modules *****
    implementation project(':delivery-application:infra-database')
    implementation project(':delivery-application:catalogue:catalogue-api')
    runtimeOnly project(':delivery-application:catalogue:catalogue-runtime')
}

// Task that runs Docker Compose
tasks.register('dockerUp', Exec) {
    group = 'infrastructure'
    description = 'Runs docker compose up -d'
    workingDir "$rootDir/delivery-infra"
    commandLine 'docker', 'compose', 'up', '-d'
}

tasks.named('bootRun') {
    group = 'Development'
    description = 'Run the Spring Boot application'
    dependsOn 'dockerUp'
    args = ["--spring.profiles.active=${project.properties['profiles'] ?: 'dev'}"]
}

tasks.named('test') {
    group = 'Development'
    description = 'Run all tests using Spock'
    dependsOn 'dockerUp'
}

tasks.named('dockerUp') {
    group = 'Development'
    description = 'Run docker-compose to start required containers'
}

tasks.named('bootJar') {
    enabled = true
}

tasks.named('jar') {
    enabled = false
}
